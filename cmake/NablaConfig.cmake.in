@PACKAGE_INIT@

set(Nabla_ROOT "${PACKAGE_PREFIX_DIR}")
set(Nabla_GIT_INFO_JSON_FILE "${PACKAGE_PREFIX_DIR}/include/nabla_git_info.json")
set(Nabla_DXC_GIT_INFO_JSON_FILE "${PACKAGE_PREFIX_DIR}/include/dxc_git_info.json")

if(NOT EXISTS "${Nabla_GIT_INFO_JSON_FILE}")
    message(FATAL_ERROR "Nabla: Git info JSON not found at \"${Nabla_GIT_INFO_JSON_FILE}\"")
endif()

if(NOT EXISTS "${Nabla_DXC_GIT_INFO_JSON_FILE}")
    message(FATAL_ERROR "Nabla: DXC Git info JSON not found at \"${Nabla_DXC_GIT_INFO_JSON_FILE}\"")
endif()

include("${CMAKE_CURRENT_LIST_DIR}/NablaExportTargets.cmake")
check_required_components(Nabla)

#
# nabla_setup_runtime_modules(<target> [RUNTIME_MODULES_SUBDIR <relative-subdir>] [INSTALL_RULES <ON|OFF>])
# nabla_setup_runtime_install_modules(<target> [RUNTIME_MODULES_SUBDIR <relative-subdir>])
#
# Runtime setup helpers for Nabla and DXC modules.
#
# Common behavior:
# - Adds runtime lookup defines on <target>:
#     NBL_CPACK_PACKAGE_NABLA_DLL_DIR="./<RUNTIME_MODULES_SUBDIR>"
#     NBL_CPACK_PACKAGE_DXC_DLL_DIR="./<RUNTIME_MODULES_SUBDIR>"
#
# nabla_setup_runtime_modules:
# - Adds build-time copy into:
#     $<TARGET_FILE_DIR:<target>>/<RUNTIME_MODULES_SUBDIR>
# - Optionally installs runtime modules when INSTALL_RULES is ON.
#
# nabla_setup_runtime_install_modules:
# - Adds install rules only (no build-time copy):
#     ${CMAKE_INSTALL_BINDIR}/<RUNTIME_MODULES_SUBDIR>
#
# Config mapping:
# - Source module path is resolved from $<TARGET_FILE:Nabla::Nabla>.
# - Imported-config mapping (MAP_IMPORTED_CONFIG_*) applies automatically.
# - If using CMAKE_MAP_IMPORTED_CONFIG_<CONFIG>, set it before find_package(Nabla).
#
function(_nbl_runtime_modules_apply_lookup_definitions _TARGET _RUNTIME_MODULES_SUBDIR)
  target_compile_definitions("${_TARGET}" PRIVATE
    NBL_CPACK_PACKAGE_NABLA_DLL_DIR="./${_RUNTIME_MODULES_SUBDIR}"
    NBL_CPACK_PACKAGE_DXC_DLL_DIR="./${_RUNTIME_MODULES_SUBDIR}"
  )
endfunction()

function(_nbl_runtime_modules_add_install_rules _RUNTIME_MODULES_SUBDIR)
  if(NOT DEFINED CMAKE_INSTALL_BINDIR)
    include(GNUInstallDirs)
  endif()

  set(_nbl_install_modules_dest "${CMAKE_INSTALL_BINDIR}/${_RUNTIME_MODULES_SUBDIR}")
  string(MD5 _nbl_install_modules_dest_key "${_nbl_install_modules_dest}")
  get_property(_nbl_install_rules_added GLOBAL PROPERTY "NBL_RUNTIME_MODULES_INSTALL_RULES_${_nbl_install_modules_dest_key}")
  if(NOT _nbl_install_rules_added)
    install(FILES "$<TARGET_FILE:Nabla::Nabla>"
      DESTINATION "${_nbl_install_modules_dest}"
    )
    install(DIRECTORY "$<PATH:APPEND,$<TARGET_FILE_DIR:Nabla::Nabla>,3rdparty,dxc>/"
      DESTINATION "${_nbl_install_modules_dest}"
    )
    set_property(GLOBAL PROPERTY "NBL_RUNTIME_MODULES_INSTALL_RULES_${_nbl_install_modules_dest_key}" TRUE)
  endif()
endfunction()

function(nabla_setup_runtime_install_modules _TARGET)
  if(NOT TARGET "${_TARGET}")
    message(FATAL_ERROR "Nabla: target \"${_TARGET}\" does not exist")
  endif()

  set(_nbl_runtime_modules_subdir "Libraries")
  set(_nbl_options "")
  set(_nbl_one_value_args RUNTIME_MODULES_SUBDIR)
  set(_nbl_multi_value_args "")
  cmake_parse_arguments(_NBL_CUSTOM "${_nbl_options}" "${_nbl_one_value_args}" "${_nbl_multi_value_args}" ${ARGN})

  if(_NBL_CUSTOM_RUNTIME_MODULES_SUBDIR)
    set(_nbl_runtime_modules_subdir "${_NBL_CUSTOM_RUNTIME_MODULES_SUBDIR}")
  endif()

  _nbl_runtime_modules_apply_lookup_definitions("${_TARGET}" "${_nbl_runtime_modules_subdir}")
  _nbl_runtime_modules_add_install_rules("${_nbl_runtime_modules_subdir}")
endfunction()

function(nabla_setup_runtime_modules _TARGET)
  if(NOT TARGET "${_TARGET}")
    message(FATAL_ERROR "Nabla: target \"${_TARGET}\" does not exist")
  endif()

  set(_nbl_runtime_modules_subdir "Libraries")
  set(_nbl_install_rules OFF)
  set(_nbl_options "")
  set(_nbl_one_value_args RUNTIME_MODULES_SUBDIR INSTALL_RULES)
  set(_nbl_multi_value_args "")
  cmake_parse_arguments(_NBL_CUSTOM "${_nbl_options}" "${_nbl_one_value_args}" "${_nbl_multi_value_args}" ${ARGN})

  if(_NBL_CUSTOM_RUNTIME_MODULES_SUBDIR)
    set(_nbl_runtime_modules_subdir "${_NBL_CUSTOM_RUNTIME_MODULES_SUBDIR}")
  endif()
  if(DEFINED _NBL_CUSTOM_INSTALL_RULES)
    set(_nbl_install_rules "${_NBL_CUSTOM_INSTALL_RULES}")
  endif()

  _nbl_runtime_modules_apply_lookup_definitions("${_TARGET}" "${_nbl_runtime_modules_subdir}")

  add_custom_command(TARGET "${_TARGET}" POST_BUILD
    COMMAND ${CMAKE_COMMAND} -E make_directory "$<TARGET_FILE_DIR:${_TARGET}>/${_nbl_runtime_modules_subdir}"
    COMMAND ${CMAKE_COMMAND} -E copy_if_different "$<TARGET_FILE:Nabla::Nabla>" "$<TARGET_FILE_DIR:${_TARGET}>/${_nbl_runtime_modules_subdir}/"
    COMMAND ${CMAKE_COMMAND} -E copy_directory "$<PATH:APPEND,$<TARGET_FILE_DIR:Nabla::Nabla>,3rdparty,dxc>" "$<TARGET_FILE_DIR:${_TARGET}>/${_nbl_runtime_modules_subdir}"
    VERBATIM
  )

  if(_nbl_install_rules)
    _nbl_runtime_modules_add_install_rules("${_nbl_runtime_modules_subdir}")
  endif()
endfunction()

if(NABLA_FIND_PACKAGE_VERBOSE)
  message(STATUS
    "\n-- Nabla_ROOT = ${Nabla_ROOT}"
    "\n-- Nabla_VERSION = ${Nabla_VERSION}"
  )

  file(READ "${Nabla_GIT_INFO_JSON_FILE}" _nabla_git_info_raw)
  file(READ "${Nabla_DXC_GIT_INFO_JSON_FILE}" _nabla_dxc_git_info_raw)
  message(STATUS
    "-- Nabla module git info:"
    "\n${_nabla_git_info_raw}"
    "-- Nabla's DXC module git info:"
    "\n${_nabla_dxc_git_info_raw}"
  )
endif()
