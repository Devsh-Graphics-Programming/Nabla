#version 450 core

layout(push_constant) uniform Block
{
	vec4 camPosGenesisVoxelExtent;
	uint hierarchyLevel;
} pc;

#include <../cull_common.glsl>

layout (set = 0, binding = 0, std430) restrict buffer readonly Lights
{
	nbl_glsl_ext_ClusteredLighting_SpotLight data[];
} lights;

layout (set = 0, binding = 1, std430) restrict buffer InScratch
{
	uint count;
	uint padding;
	uvec2 data[];
} inScratch;

layout (set = 0, binding = 2, std430) restrict buffer writeonly IntersectionRecords
{
	uint count;
	uint padding;
	uvec2 data[];
} intersectionRecords;

layout (set = 0, binding = 3, r32ui) restrict uniform uimage3D lightGrid;

shared uint scratchShared[_NBL_GLSL_WORKGROUP_SIZE_*2+2];

void main()
{
	const float genesisVoxelExtent = pc.camPosGenesisVoxelExtent.w;
	const uint recordsSpawnedPerWGOffset = _NBL_GLSL_WORKGROUP_SIZE_*2;
	const uint persistentWGStepWidth = gl_NumWorkGroups.x * LIGHTS_PER_WORKGROUP;
	const uint persistentWGStepCount = uint(ceil(float(inScratch.count)/float(persistentWGStepWidth)));

	for (uint step = 0u; step < persistentWGStepCount; ++step)
	{
		const uint index = step*persistentWGStepWidth + gl_WorkGroupID.x*LIGHTS_PER_WORKGROUP + gl_LocalInvocationIndex;

		if ((gl_LocalInvocationIndex < LIGHTS_PER_WORKGROUP) && (index < inScratch.count))
		{
			const uvec2 packedIntersectionRecord = inScratch.data[index];
			scratchShared[gl_LocalInvocationIndex] = packedIntersectionRecord.x;
			scratchShared[gl_LocalInvocationIndex + _NBL_GLSL_WORKGROUP_SIZE_] = packedIntersectionRecord.y;
		}

		if (gl_LocalInvocationIndex == LIGHTS_PER_WORKGROUP)
			scratchShared[recordsSpawnedPerWGOffset] = 0u;
		barrier();

		const uint localLightIndex = gl_LocalInvocationIndex/INVOCATIONS_PER_LIGHT;
		const uvec2 packedIntersectionRecord = uvec2(scratchShared[localLightIndex], scratchShared[localLightIndex + _NBL_GLSL_WORKGROUP_SIZE_]);
		barrier();

		if (index < inScratch.count)
		{
			const uint globalLightIndex = getGlobalLightIndex(packedIntersectionRecord);

			const nbl_glsl_ext_ClusteredLighting_SpotLight light = lights.data[globalLightIndex];
			const float importanceMagnitude = getLightImportanceMagnitude(light);

			// Todo(achal): This threshold value must come from the outside
			// if (importanceMagnitude < threshold)
			// 	break;

			const cone_t cone = getLightVolume(light);

			const float voxelSideLength = genesisVoxelExtent/float(1<<pc.hierarchyLevel);
			const vec3 levelMinVertex = vec3(-genesisVoxelExtent/2.f);

			const uvec3 parentVoxelID = getLocalClusterID(packedIntersectionRecord);

			const uvec3 voxelID = (parentVoxelID << 1u) + ((uvec3(gl_LocalInvocationIndex)>>uvec3(0,1,2))&0x1u);
			const nbl_glsl_shapes_AABB_t cluster = getCluster(voxelID, levelMinVertex, voxelSideLength);

			if (coneIntersectAABB(cone, cluster))
			{
				const uint localOffset = atomicAdd(scratchShared[recordsSpawnedPerWGOffset], 1u);

				// repack outputs for coalesced write
				intersection_record_t record;
				record.localClusterID = voxelID;
				record.level = pc.hierarchyLevel;
				record.localLightIndex = imageAtomicAdd(lightGrid, ivec3(voxelID), 1u);
				record.globalLightIndex = globalLightIndex;
				const uvec2 packed = packIntersectionRecord(record);

				scratchShared[localOffset] = packed.x;
				scratchShared[localOffset+_NBL_GLSL_WORKGROUP_SIZE_] = packed.y;
			}
		}
		barrier();

		// each light can spawn `INVOCATIONS_PER_LIGHT` copies of itself (which are found to be intersecting the children light grid nodes of the node the current light reference is referencing)
		const uint lightReferencesSpawned = scratchShared[recordsSpawnedPerWGOffset];
		const bool invocationWillWrite = gl_LocalInvocationIndex<lightReferencesSpawned;

		// elect one invocation && best to avoid a +0 on an atomic
		if (gl_LocalInvocationIndex==0u && invocationWillWrite)
			scratchShared[recordsSpawnedPerWGOffset+1u] = atomicAdd(intersectionRecords.count, lightReferencesSpawned);
		barrier();

		if (invocationWillWrite)
		{
			uint budgetIntersectionRecordCapacity = MEMORY_BUDGET/8u;

			const uint baseOffset = scratchShared[recordsSpawnedPerWGOffset+1u];
			const uint outIndex = baseOffset + gl_LocalInvocationIndex;
			// if (outIndex < budgetIntersectionRecordCapacity)
				intersectionRecords.data[outIndex] = uvec2(scratchShared[gl_LocalInvocationIndex],scratchShared[gl_LocalInvocationIndex+_NBL_GLSL_WORKGROUP_SIZE_]);
		}
	}

	// Todo(achal): This is wrong, need to get rid of it
	memoryBarrier();
	if (gl_GlobalInvocationID.x == 0u)
		inScratch.count = 0u;
}