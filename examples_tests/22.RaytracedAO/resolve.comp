#version 430 core
#include "raytraceCommon.glsl"

#include <nbl/builtin/glsl/ext/MitsubaLoader/instance_data_descriptor.glsl>

#include <nbl/builtin/glsl/ext/RadeonRays/intersection.glsl>
layout(set = 3, binding = 0, std430) restrict buffer Queries
{
	nbl_glsl_ext_RadeonRays_Intersection hits[];
};
layout(set = 3, binding = 1, r32ui) restrict uniform uimage2D framebuffer;


void main()
{
	ivec2 pixelCoord = ivec2(gl_GlobalInvocationID.xy);
	const uint baseID = pixelCoord.y*staticViewData.samplesPerRowPerDispatch+pixelCoord.x*staticViewData.samplesPerPixelPerDispatch;
	if (all(lessThan(pixelCoord,staticViewData.imageDimensions)))
	{
		vec3 acc = fetchAccumulation(pixelCoord);
		
		vec3 color = vec3(0.0);
		uvec2 groupLocation = gl_WorkGroupID.xy*gl_WorkGroupSize.xy;
		for (uint i=0u; i<staticViewData.samplesPerPixelPerDispatch; i++)
		{
			const uint rayID = baseID+i;
			nbl_glsl_ext_RadeonRays_Intersection hit = hits[rayID];

			const vec3 thoughput = vec3(unpackHalf2x16(rays[rayID].useless_padding).yx,unpackHalf2x16(rays[rayID].backfaceCulling).yx);
			// init to miss color
			vec3 emissive = staticViewData.envmapBaseColor;
			// actually got a hit	
			if (hit.shapeid>=0)
			{
				// TODO: how to determine frontface easily from radeon rays!? barycentrics? ray direction vs. oriented cross product?
				const bool frontface = true;

				// hit buffer needs clearing
				hits[rayID].shapeid = -1;

				nbl_glsl_MC_oriented_material_t material = nbl_glsl_MC_material_data_t_getOriented(InstData.data[hit.shapeid].material,frontface);
				emissive = nbl_glsl_MC_oriented_material_t_getEmissive(material);
			}
			color += emissive*thoughput;
		}
		if (pc.cummon.rcpFramesDispatched<1.0)
			acc += (color-acc)*pc.cummon.rcpFramesDispatched;
		else
			acc += color;
		storeAccumulation(acc,pixelCoord);
		imageStore(framebuffer,pixelCoord,uvec4(nbl_glsl_encodeRGB10A2(vec4(acc,1.0)),0u,0u,0u));
	}
}