#version 430 core

// For a 1024x1024 image
// WG_SIZE = 256x1x1
// VT = 4
// Number of workgroups = 1024

#define _NBL_GLSL_WORKGROUP_SIZE_ 256

// Todo: DRY!!
#define WIDTH 1024
#define HEIGHT 1024
#define RADIUS 15.73f

// This would probably be a push constant
#define VT 4

layout (local_size_x = _NBL_GLSL_WORKGROUP_SIZE_) in;

layout (set = 0, binding = 0, std430) restrict readonly buffer samples_buffer
{
	uvec2 sample_values[];
};

layout (set = 0, binding = 1, rgba16f) writeonly uniform image2D out_image;

vec3 decodeRGB(uvec2 val)
{
    vec3 result;
    result.rg = unpackHalf2x16(val.x);
    result.b = unpackHalf2x16(val.y).x;

    return result;
}

#include <nbl/builtin/glsl/workgroup/shared_arithmetic.glsl>

#if HEIGHT < _NBL_GLSL_WORKGROUP_ARITHMETIC_SHARED_SIZE_NEEDED_
	#define _NBL_GLSL_EXT_BLUR_SHARED_SIZE_NEEDED_ _NBL_GLSL_WORKGROUP_ARITHMETIC_SHARED_SIZE_NEEDED_
#else
	#define _NBL_GLSL_EXT_BLUR_SHARED_SIZE_NEEDED_ HEIGHT
#endif

#ifdef _NBL_GLSL_SCRATCH_SHARED_SIZE_DEFINED_
	#if NBL_GLSL_EVAL(_NBL_GLSL_SCRATCH_SHARED_SIZE_DEFINED_) < NBL_GLSL_EVAL(_NBL_GLSL_EXT_BLUR_SHARED_SIZE_NEEDED_)
		#error "Not enough shared memory declared"
	#endif
#else
    #define _NBL_GLSL_SCRATCH_SHARED_SIZE_DEFINED_ _NBL_GLSL_EXT_BLUR_SHARED_SIZE_NEEDED_
    #define _NBL_GLSL_SCRATCH_SHARED_DEFINED_ nbl_glsl_ext_Blur_scratchShared
	shared uint _NBL_GLSL_SCRATCH_SHARED_DEFINED_[_NBL_GLSL_SCRATCH_SHARED_SIZE_DEFINED_];
#endif

// Todo: Need to test and tighten this whole spillage calculation up
#define _NBL_GLSL_EXT_BLUR_IMPL_LOCAL_SPILLAGE_ ((_NBL_GLSL_WORKGROUP_ARITHMETIC_SHARED_SIZE_NEEDED_-1)/_NBL_GLSL_WORKGROUP_SIZE_+1)

#if _NBL_GLSL_EXT_BLUR_IMPL_LOCAL_SPILLAGE_ > VT
	#define _NBL_GLSL_EXT_BLUR_LOCAL_SPILLAGE_ VT
#elif _NBL_GLSL_EXT_BLUR_IMPL_LOCAL_SPILLAGE_ < (VT/4)
	#define _NBL_GLSL_EXT_BLUR_LOCAL_SPILLAGE_ (VT/4)
#else
	#define _NBL_GLSL_EXT_BLUR_LOCAL_SPILLAGE_ _NBL_GLSL_EXT_BLUR_IMPL_LOCAL_SPILLAGE_
#endif

#include <nbl/builtin/glsl/workgroup/arithmetic.glsl>
#include <nbl/builtin/glsl/workgroup/ballot.glsl>

// Convenience thing
#define scratch_shared _NBL_GLSL_SCRATCH_SHARED_DEFINED_

// Note: This function depends on `scratch_shared` to be properly initialized with inclusive prefix sums
// Todo(achal): You can't have it depend on `scratch_shared` --das not gud
float getInterpolatedPrefixSums(float idx)
{
	uint floored = uint(floor(idx));
	return mix(uintBitsToFloat(scratch_shared[floored]), uintBitsToFloat(scratch_shared[floored + 1u]), fract(idx));
}

vec3 getPaddedData(in uint i)
{
	uint idx = (i * _NBL_GLSL_WORKGROUP_SIZE_) + gl_LocalInvocationIndex; // y coordinate

	vec3 data = vec3(0.f);
	if (idx < HEIGHT && gl_WorkGroupID.x < WIDTH)
		data = decodeRGB(sample_values[idx * WIDTH + gl_WorkGroupID.x]);

	return data;
}

void main()
{
	const uint channel_count = 3u;
	const uint pass_count = 3u;

	vec3 blurred[VT];
	for (uint i = 0u; i < VT; ++i)
		blurred[i] = getPaddedData(i);

	for (uint ch = 0; ch < channel_count; ++ch)
	{
		for (uint pass = 0; pass < pass_count; ++pass)
		{
			float previous_block_sum = 0.f;
	
			float spill[_NBL_GLSL_EXT_BLUR_LOCAL_SPILLAGE_];
			for (uint i = 0u; i < _NBL_GLSL_EXT_BLUR_LOCAL_SPILLAGE_; ++i)
			{
			    spill[i] = nbl_glsl_workgroupInclusiveAdd(blurred[i][ch]) + previous_block_sum;
			    previous_block_sum = nbl_glsl_workgroupBroadcast(spill[i], _NBL_GLSL_WORKGROUP_SIZE_ - 1u);
			}
	
			for (uint i = _NBL_GLSL_EXT_BLUR_LOCAL_SPILLAGE_; i < VT; ++i)
			{
			    float scan_result = nbl_glsl_workgroupInclusiveAdd(blurred[i][ch]) + previous_block_sum;
			    previous_block_sum = nbl_glsl_workgroupBroadcast(scan_result,  _NBL_GLSL_WORKGROUP_SIZE_ - 1u);
	
				uint idx = (i * _NBL_GLSL_WORKGROUP_SIZE_) + gl_LocalInvocationIndex;
			    scratch_shared[idx] = floatBitsToUint(scan_result);
			}
	
			for (uint i = 0u; i < _NBL_GLSL_EXT_BLUR_LOCAL_SPILLAGE_; ++i)
			    scratch_shared[(i * _NBL_GLSL_WORKGROUP_SIZE_) + gl_LocalInvocationIndex] = floatBitsToUint(spill[i]);
			barrier();
			
			for (uint i = 0; i < VT; ++i)
			{
				uint idx = (i * _NBL_GLSL_WORKGROUP_SIZE_) + gl_LocalInvocationIndex;
				if (idx < HEIGHT)
				{
					float left = float(idx) - RADIUS - 1.f;
					float right = float(idx) + RADIUS;
	
					const uint last = HEIGHT - 1u;
	
					float result;
					if (right > last)
						result = (right - float(last)) * (uintBitsToFloat(scratch_shared[last]) - uintBitsToFloat(scratch_shared[last - 1u])) + uintBitsToFloat(scratch_shared[last]);
					else
						result = getInterpolatedPrefixSums(right);
	
					if (left < 0)
						result -= (1.f - abs(left)) * uintBitsToFloat(scratch_shared[0u]);
					else
						result -= getInterpolatedPrefixSums(left);
	
					blurred[i][ch] = result/(2.f*RADIUS + 1.f);
				}
			}
		}
	}

	for (uint i = 0; i < VT; ++i)
	{
		uint idx = (i * _NBL_GLSL_WORKGROUP_SIZE_) + gl_LocalInvocationIndex;
		if (idx < HEIGHT && gl_WorkGroupID.x < WIDTH)
			imageStore(out_image, ivec2(gl_WorkGroupID.x, idx), vec4(blurred[i], 1.f));
	}
}
























